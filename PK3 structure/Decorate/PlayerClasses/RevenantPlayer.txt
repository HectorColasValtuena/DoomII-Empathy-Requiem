////////////////////////////////////////////////////
/////////////////// REVENANT ///////////////////////
////////////////////////////////////////////////////

////////////////////////////////////////////////////
////  Defines / Variables

const float RevenantMissileOuterSpread = 30;
const float RevenantMissileInnerSpread = 3;

////////////////////////////////////////////////////
////  Player actor class

actor RevenantPlayer : BaseEmpathyPlayer
{
  Speed 1.0
	Health 300
	Radius 20
	Height 56
	Mass 500
	PainChance 255
	player.attackzoffset 8
	player.maxhealth 300
	player.soundclass "RevenantPlayerSnd"
	player.morphweapon "RevenantCannons"
	Player.ColorRange 0, 0
	Player.DisplayName "Revenant"
	Player.StartItem "RevenantCannons"
	Player.StartItem "PlayerIsRevenant"
	Player.StartItem "RevenantSeekerAmmo", 120
        
	States
	{
  	Spawn:
  		SKEL A 9
  		SKEL B 9
  		SKEL B 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
  		loop

  	See:
  		SKEL A 3  //mana regeneration is offset 1 animation frame to prevent animation spamming from accelerating actual regeneration
      SKEL BCDEF 3 A_GiveInventory ("RevenantSeekerAmmo", 1)
      SKEL A 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
      loop
      /*SKEL D 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
      SKEL CD 3
      SKEL A 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
      SKEL EF 3
      SKEL A 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
  		Loop*/

  	Melee: //actually missile animation
      SKEL J 4 bright
      SKEL K 18
  		goto spawn
  	
  	Missile: //actually melee animation
      SKEL GHI 6
  		goto spawn

  	Pain:
  		SKEL L 5
      SKEL L 5 A_Pain
  		goto spawn

  	Death:
  		SKEL L 7 A_PlayerSkinCheck("AltSkinDeath")
  		SKEL M 7
  		SKEL N 7 A_PlayerScream
  		SKEL O 7 A_NoBlocking
   		SKEL P 7
   		SKEL Q -1
  		stop

  	XDeath:
  		goto death

  	AltSkinDeath:
  		SKEL L 7
  		SKEL M 7
  		SKEL N 7 A_PlayerScream
  		SKEL O 7 A_NoBlocking
   		SKEL P 7
   		SKEL Q -1
  		stop
	}
}


////////////////////////////////////////////////////
////  Weapon(s) Actor Class

actor RevenantCannons : weapon
{
  radius 20
  height 20
  obituary "%o couldn't evade a k%'s tracer"
  inventory.pickupmessage "Feel the power of a Revenant"
  attacksound "skeleton/melee"
  weapon.selectionorder 5000
  weapon.kickback 500
  weapon.ammotype "RevenantSeekerAmmo"
  weapon.ammouse1 1
  weapon.ammouse2 0
  weapon.ammogive 100
  +WEAPON.DONTBOB
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.ALT_AMMO_OPTIONAL
  +WEAPON.NODEATHDESELECT

  States
  {
    Deselect: 
      REVC A 0 
      goto Ready
    Select: 
      REVC A 1 
      goto Ready

    Ready: 
      REVC A 18 A_WeaponReady
      //REVC A 0 A_GiveInventory ("RevenantSeekerAmmo", 1)
      loop
    /*  //old fire
    Fire:
      REVC A 0 A_JumpIfNoAmmo ("AltFire")
      REVC B 2 A_GunFlash
      REVC C 2 A_FireCustomMissile ("RevenantPlayerSeeker")
      REVC DE 2 
      REVC A 5 A_ReFire 
      Goto Ready
    */

    Fire:
      REVC A 0 A_JumpIfNoAmmo ("AltFire")
      REVC A 0 A_Jump (128, "RightFire")  //choose initial side randomly (50%)
      goto LeftFire

    RightFire:
      REVC A 0 A_JumpIfNoAmmo ("AltFire")
      REVC B 1 A_GunFlash
      REVC C 1 A_FireProjectile ("RevenantPlayerSeeker", frandom(-RevenantMissileOuterSpread, RevenantMissileInnerSpread), true, 16, 4)
      REVC D 1
      REVC A 1 A_ReFire("LeftFire")
      goto Ready

    LeftFire:
      REVC A 0 A_JumpIfNoAmmo ("AltFire")
      REVC E 1 A_GunFlash
      REVC F 1 A_FireProjectile ("RevenantPlayerSeeker", frandom(-RevenantMissileInnerSpread, RevenantMissileOuterSpread), true, -16, 4)
      REVC G 1
      REVC A 1 A_ReFire("RightFire")
      goto Ready


    Flash:
      TNT1 A 2 bright A_Light1
      TNT1 A 0 A_Light0
      stop

    //Old alt fire
    AltFire:
      REVO F 6 A_PlaySoundEx("skeleton/swing","Weapon")
      REVO G 6 
      REVO H 6 A_CustomPunch (8)
      REVO IJ 5
      REVO A 0 A_ReFire
      goto ready
  }
}


////////////////////////////////////////////////////
////  Projectile actor class

ACTOR RevenantPlayerSeeker
{
  Radius 4
  Height 8
  Speed 10
  Damage 5 //original is 10
  Projectile
  SeeSound "skeleton/attack"
  DeathSound "skeleton/tracex"
  RenderStyle Add
  Alpha 0.95
  +SEEKERMISSILE
  +RANDOMIZE
  +DONTSEEKINVISIBLE
  States
  {
    //A_SeekerMissile (angle threshold, angle maxturnangle, int flags = 0, int chance = 50, int distance = 10)
    Spawn:
      FATB AB 2 Bright A_SeekerMissile (3, 18, (SMF_LOOK | SMF_PRECISE), 100)
      loop

    Death:
      FBXP A 6 Bright
      FBXP B 4 Bright
      FBXP C 2 Bright
      stop
  }
}

////////////////////////////////////////////////////
////  Ammo actor class

actor RevenantSeekerAmmo : ammo
{
  inventory.amount 120
  inventory.maxamount 120
  inventory.icon "ROCKA0"
  //RENDERSTYLE TRANSLUCENT
  //ALPHA 0.5
  states
  {
    Spawn:
      ROCK A -1
      stop
  }
}

