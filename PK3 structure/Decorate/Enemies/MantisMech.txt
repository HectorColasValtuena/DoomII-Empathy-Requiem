actor MantisMech : EmpathyBaseEnemyMediumHuman replaces HellKnight 16051
{
  Health 600
  Radius 20
  Height 60
  Speed 8
  PainChance 64

  MaxDropOffHeight 32
  MinMissileChance 200
  MeleeRange 48

  +NOBLOOD
  +NOSPLASHALERT
  //+MISSILEMORE
  //+MISSILEEVENMORE

  SeeSound "enemy/mantis/sight"
  AttackSound "enemy/mantis/shot"
  PainSound "enemy/mantis/pain"
  DeathSound "enemy/mantis/death"
  ActiveSound "enemy/mantis/active"
  Obituary "%o got ripped by a Mantis Mech."
  
  States
  {
    Spawn:
      PGRD A 5 A_Look
      Loop
      
    See:
      PGRD AABBCCDD 3 A_Chase
      Loop

    Melee:
      PGRD E 6 A_FaceTarget
      PGRD F 6 A_CustomMeleeAttack((random(2, 8)*5), "enemy/mantis/melee")
      PGRD E 6 
      Goto See

      //A_SpawnProjectile (string missiletype, float spawnheight, float spawnofs_xy, double angle, int flags, angle pitch, int ptr)
    Missile:
      PGRD G 8 Bright A_FaceTarget
      PGRD H 8 Bright light("GreenPlasmaHelixShot")
      PGRD H 0 A_SpawnProjectile("GreenPlasmaHelixBall0")
      PGRD H 0 A_SpawnProjectile("GreenPlasmaHelixBall1")
      PGRD H 0 A_SpawnProjectile("GreenPlasmaHelixBall2")
      PGRD H 0 A_SpawnProjectile("GreenPlasmaHelixBall3")
      Goto See

    Pain:
      PGRD A 2
      PGRD A 2 A_Pain
      Goto See

    Death:
      PGRD I 4 A_TossGib
      PGRD J 4 A_Scream
      PGRD K 4 A_TossGib
      PGRD L 4 A_NoBlocking
      PGRD MN 4
      PGRD O 4 A_TossGib
      PGRD "PQRSTUVWXYZ[" 4
      PGRD "]" -1
      Stop
  }
}