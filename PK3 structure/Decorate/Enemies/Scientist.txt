const int ScientistWarnRange = 256;
const int ScientistWarnChance = 25;
const int ScientistHasGunChance = 50;

actor Scientist : EmpathyBaseEnemySmallHuman replaces ZombieMan 16001
{
  Health 10
  Radius 20
  Height 56
  Speed 12
  PainChance 255
  
  SeeSound "enemy/scientist/sight"
  AttackSound "enemy/guard/shot"
  PainSound "enemy/scientist/pain"
  DeathSound "enemy/scientist/death"
  //ActiveSound "enemy/scientist/sight"
  Obituary "%o managed to get killed by an untrained scientist. Somehow."
  
  +FRIGHTENED

  States
  {
    Spawn:
      SCIE A 0
      SCIE A 0 {
      	A_TakeInventory("ScientistHasGun", 0x7fffffff);
        if (random(1,100) <= ScientistHasGunChance) {	//give the scientist a gun with roughly a 1/3 chance
          A_GiveInventory("ScientistHasGun", 1);
        }
      }
      goto SpawnLoop

    SpawnLoop:
      SCIE A 10 A_Look
      loop

    See:
      SCIE BBCCDDEE 4 A_Chase
      SCIE B 0 {
      	if (random(1,100) <= ScientistWarnChance) {
	      A_PlaySound("enemy/scientist/sight");
	      A_AlertMonsters(ScientistWarnRange);
      	}
      }
      Loop

    Missile:	//on the attack state, warn every nearby enemy but actually attack only if the scientist has a gun (1/3 chance)
      SCIE F 0 {
      	A_PlaySound("enemy/scientist/sight");
      	A_AlertMonsters(ScientistWarnRange);
      }
      SCIE F 0 A_JumpIfInventory("ScientistHasGun", 1, "Shot")
      goto See

    Shot:
      SCIE LM 8 A_FaceTarget
      SCIE N 8 bright light("PlasmaPistolShot") A_SpawnProjectile("PlasmaGunBall", 36, 4)
      SCIE ML 4
      goto See

    Pain:
      SCIE Z 3
      SCIE Z 3 A_Pain
      Goto See

    Death:
      SCIE F 6
      SCIE G 6 A_Scream
      SCIE H 6 A_NoBlocking
      SCIE I 6
      SCIE J 6
      SCIE K -1
      Stop
 
    XDeath:
      goto Death

    Raise:
      SCIE KJIHGF 4
      Goto See
  }

}

actor ScientistHasGun : Inventory //this will be used to determine if the scientist carries a gun
{
  inventory.amount 1
  inventory.maxamount 1
  /*+INVENTORY.QUIET
  +INVENTORY.UNDROPPABLE
  +INVENTORY.ALWAYSPICKUP
  +INVENTORY.IGNORESKILL
  */
}