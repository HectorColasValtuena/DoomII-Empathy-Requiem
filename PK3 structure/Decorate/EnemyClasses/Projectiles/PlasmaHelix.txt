////////////////////////////////////////////////////
////  Plasma green helix

const float GPHHomingRate = 1;

const int GPHWeaveSpeed = 2;
const float GPHWeaveDist = 2.0; //4.0;
const float GPHWeaveDistXY = 2.237; //4.474; 

actor GreenPlasmaHelixBall : FastProjectile
{
  Radius 4
  Height 6
  Speed 20
  FastSpeed 20
  Damage (random(2, 8) * 5)
  Projectile
  //+RANDOMIZE
  RenderStyle Add
  Alpha 0.9
  Scale 0.5
  SeeSound "enemy/mantis/shot"
  DeathSound "weapons/plasmax"

  MissileType "GreenPlasmaHelixBallTrail"
  MissileHeight 8

  WeaveIndexXY 0
  WeaveIndexZ 0

  +SEEKERMISSILE

  States
  {
    Spawn:
      GPLB AAAABBBB 1 bright light("GreenPlasmaHelixBall") {
        A_Weave(GPHWeaveSpeed, GPHWeaveSpeed, GPHWeaveDist, GPHWeaveDist);
        A_SeekerMissile (0, GPHHomingRate, (SMF_CURSPEED));
      }
      Loop
    Death:
      GPLB A 4 bright light("GreenPlasmaHelixExp2")
      GPLB BC 4 bright light("GreenPlasmaHelixExp1")
      GPLB D 4 bright light("GreenPlasmaHelixExp2")
      GPLB E 4 Bright light("GreenPlasmaHelixBall")
      Stop
  }
}
  actor GreenPlasmaHelixBallVert : GreenPlasmaHelixBall {
    States {
      Spawn:
        GPLB AAAABBBB 1 bright light("GreenPlasmaHelixBall") {
          A_Weave(0, GPHWeaveSpeed, 0.0, GPHWeaveDistXY);
          A_SeekerMissile (0, GPHHomingRate, (SMF_CURSPEED));
        }
        Loop
    }
  }
  actor GreenPlasmaHelixBallHorz : GreenPlasmaHelixBall {
    States {
      Spawn:
        GPLB AAAABBBB 1 bright light("GreenPlasmaHelixBall") {
          A_Weave(GPHWeaveSpeed, 0, GPHWeaveDistXY, 0.0);
          A_SeekerMissile (0, GPHHomingRate, (SMF_CURSPEED));
        }
        Loop
    }
  }

actor GreenPlasmaHelixBallTrail
{
  Radius 4
  Height 6
  RenderStyle Add
  Scale 0.5
  Alpha 0.5

  +NOGRAVITY
  +NOBLOCKMAP
  +NOCLIP

  States
  {
    Spawn:
      GPLB B 1 Bright { A_SetScale(0.50); }
      GPLB A 1 Bright { A_SetScale(0.475); }
      GPLB A 1 Bright { A_SetScale(0.45); }
      GPLB B 1 Bright { A_SetScale(0.425); }
      GPLB B 1 Bright { A_SetScale(0.40); }
      GPLB A 1 Bright { A_SetScale(0.375); }
      GPLB A 1 Bright { A_SetScale(0.35); }
      GPLB B 1 Bright { A_SetScale(0.325); }
      GPLB B 1 Bright { A_SetScale(0.30); }
      GPLB A 1 Bright { A_SetScale(0.275); }
      GPLB A 1 Bright { A_SetScale(0.25); }
      GPLB B 1 Bright { A_SetScale(0.225); }
      GPLB B 1 Bright { A_SetScale(0.20); }
      GPLB A 1 Bright { A_SetScale(0.175); }
      GPLB A 1 Bright { A_SetScale(0.15); }
      GPLB B 1 Bright { A_SetScale(0.125); }
      GPLB B 1 Bright { A_SetScale(0.10); }
      GPLB A 1 Bright { A_SetScale(0.075); }
      GPLB A 1 Bright { A_SetScale(0.05); }
      GPLB B 1 Bright { A_SetScale(0.025); }
    stop
  }
}

actor GreenPlasmaHelixBall0 : GreenPlasmaHelixBall {
  WeaveIndexXY 0
  WeaveIndexZ 0
}

actor GreenPlasmaHelixBall1 : GreenPlasmaHelixBall {
  WeaveIndexXY 32
  WeaveIndexZ 0
}

actor GreenPlasmaHelixBall2 : GreenPlasmaHelixBall {
  WeaveIndexXY 32
  WeaveIndexZ 32
}

actor GreenPlasmaHelixBall3 : GreenPlasmaHelixBall {
  WeaveIndexXY 0
  WeaveIndexZ 32
}

actor GreenPlasmaHelixBall4 : GreenPlasmaHelixBallVert {
  WeaveIndexXY 0
  WeaveIndexZ 0
}

actor GreenPlasmaHelixBall5 : GreenPlasmaHelixBallVert {
  WeaveIndexXY 0
  WeaveIndexZ 32
}

actor GreenPlasmaHelixBall6 : GreenPlasmaHelixBallHorz {
  WeaveIndexXY 0
  WeaveIndexZ 0
}

actor GreenPlasmaHelixBall7 : GreenPlasmaHelixBallHorz {
  WeaveIndexXY 32
  WeaveIndexZ 0
}
